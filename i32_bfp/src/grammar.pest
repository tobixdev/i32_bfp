action = { SOI ~ (function_def | query | command) ~ EOI }

function_def = { ID ~ "(" ~ ID? ~ ")" ~ ":=" ~ expr }
query = { expr }
command = { show_code_command | list_fn_command | delete_fn_command | mode_command | executor_command }
show_code_command = { ".code" ~ ID }
list_fn_command = { ".list" }
delete_fn_command = { ".delete" ~ ID }
mode_command = { ".mode" ~ mode }
mode = { "proof" | "fast" }
executor_command = { ".executor" ~ executor }
executor = { "compiled" | "interpreted" }

expr = { relation }

relation = {addsub ~ (relator ~ relation)? }
relator = { "=" | "<>" }
addsub = { muldiv ~ (addsub_op ~ addsub)?  }
addsub_op = { "+" | "-" }
muldiv = { atom ~ (muldiv_op ~ muldiv)? }
muldiv_op = { "*" | "/" }
atom = {
    "(" ~ expr ~ ")" |
    NUMBER |
    function_call |
    ID
}
function_call = { ID ~ "(" ~ expr? ~ ")" }

ID = { ASCII_ALPHA+ }
NUMBER = { ASCII_DIGIT+ }
WHITESPACE = _{ " " }